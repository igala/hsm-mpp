plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.70'
    id "base"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}
repositories {
    jcenter()
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }


}


apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def GROUP_ID='de.artcom.hsm' // Your orginization in reverse domain notation. For example:  def GROUP_ID="com.fudge"
def ARTIFACT_ID="hsm-mpp-lib" // The name you gave your package in bintray. In our example it would be:  def ARTIFACT_ID="kotlin-awesome-printer"

def BINTRAY_REPOSITORY='hsm-mpp-lib' // What you named your repository in bintray. In our example it would be "myrepository"
//def BINTRAY_ORGINIZATION= //  Your public facing Bintray name. You can see your Bintray name on the top right corner in Bintray. I named it "guide" for our example.

def ISSUE_URL= 'https://github.com/igala/hsm-mpp'// A link to an issue tracker for the library, in our example: "https://github.com/natanfudge/KotlinAwesomePrinter/issues"
def SITE_URL='https://github.com/igala/hsm-mpp' // A link to the library's site. In our example: "https://github.com/natanfudge/KotlinAwesomePrinter"
def VCS_URL='https://github.com/igala/hsm-mpp' // A link to the version control system (where the code is stored). In our example: "https://github.com/natanfudge/KotlinAwesomePrinter.git"
def LIBRARY_VERSION_NAME='1.0.5' // The current version of your library. You will need to update this value whenever you upload a new release. For example: 1.0



group GROUP_ID
version LIBRARY_VERSION_NAME


kotlin {
    jvm()
    // This is for iPhone emulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosArm64("ios") {
        binaries {
            framework()
        }
    }



    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation 'co.touchlab:stately-concurrency:1.0.0-a2'
                implementation 'co.touchlab:stately-common:1.0.0-a2'
                implementation 'co.touchlab:stately-isolate:1.0.0-a2'
                implementation 'co.touchlab:stately-iso-collections:1.0.0-a2'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'

//                implementation "co.touchlab:testhelp:0.3.3"
                //implementation kotlin('test-common')
                //implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation 'ch.qos.logback:logback-classic:1.1.2'
                implementation 'ch.qos.logback:logback-core:1.1.2'
                implementation 'org.hamcrest:hamcrest-integration:1.3'
                implementation 'org.hamcrest:hamcrest-core:1.3'
                implementation 'org.hamcrest:hamcrest-library:1.3'

                implementation 'org.codehaus.groovy:groovy-all:2.4.0'
                implementation 'org.spockframework:spock-core:0.7-groovy-2.0'

                implementation 'junit:junit:4.12'

                implementation 'org.mockito:mockito-core:1.10.8'
            }
        }
        iosMain {
        }
        iosTest {

        }
    }
}

task iosTest {
    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
    dependsOn kotlin.targets.ios.binaries.getTest('DEBUG').linkTaskName
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = "Runs tests for target 'ios' on an iOS simulator"

    doLast {
        def binary = kotlin.targets.ios.binaries.getTest('DEBUG').outputFile
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
        }
    }
}


configurations {
    compileClasspath
}

afterEvaluate {
    project.publishing.publications.all {
        // rename artifacts
        groupId = GROUP_ID
        if (it.name.contains('metadata')) {
            artifactId = "$ARTIFACT_ID"
        } else {
            artifactId = "$ARTIFACT_ID-$name"
        }
    }
}

def getBintrayUserProperty() {
    return hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
}

def getBintrayApiKeyProperty() {
    return hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
}

bintray {
    //A user name of bintray to A, and API key of Bintray to B.I do not want to include API Key in git,
    // so I am gradle.properties writing locally.
    // Gradle's property file is read in the order of gradle in the home directory> gradle in the project directory,
    // so if it is written in property in the home directory, it will be useful for other projects as well.
    user = getBintrayUserProperty()
    key = getBintrayApiKeyProperty()
    //f you set publish to true, the new version will be published the moment you upload to bintray. If false, it will not be published unless you press the publish button on the bintray web.
    // It is recommended that you make it false because it can prevent an accident that you accidentally release the latest version.
    publish = true

    pkg {
        repo = BINTRAY_REPOSITORY
        name = ARTIFACT_ID
        //userOrg = BINTRAY_ORGINIZATION
        licenses = ['MIT']
        vcsUrl = VCS_URL
        websiteUrl = SITE_URL
        issueTrackerUrl = ISSUE_URL

        version {
            name = LIBRARY_VERSION_NAME
            vcsTag = LIBRARY_VERSION_NAME
            released = new Date()
        }
    }
}

// This specifies the publications to upload to bintray before doing the bintrayUpload gradle task.
//In the mpp configuration of one module after 1.3,
// it is not uploaded unless you explicitly specify artifacts in publications like this.
// It is not necessary to have the findAll part,
// [project name]-kotlinMultiplatformbut a directory containing only pom files will be uploaded, so it is better to exclude it.
bintrayUpload.doFirst {
    publications = publishing.publications.collect {
        it.name
    }.findAll {
        it != "kotlinMultiplatform"
    }
}


